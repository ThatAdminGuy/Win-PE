//Edit by caocaofff
CALL @UEFIX

_SUB UEFIX,W283H160,UEFI boot repair,,SHELL32.DLL#46
FDRV AllDrive=
ENVI WinDrv=
GROU Group1,L8T8W184H120,collect information
LABELabel1,L16T28W133H15,ESP partition:
LIST ESP,L65T24W50H600,%AllDrive%,,
LABE L1,L16T53W133H15,(Can't find the ESP partition? Try
ITEM Button1,L145T49W30H20,Mount,CALL MountESP,,0
LABE L2,L177T53W7H15,）
LABE Label2,L16T80W138H16,Windows system root directory:
ITEM WIN,L126T75W30H20,...,CALL SelectWinDrv,,0
LABE Label3,L16T106W138H16,System language:
LIST LAN,L72T102W60H600,cs-CZ|da-DK|de-DE|el-GR|en-US|es-ES|fi-FI|fr-FR|hu-HU|it-IT|ja-JP|ko-KR|nb-NO|nl-NL|pl-PL|pt-BR|pt-PT|ru-RU|sv-SE|tr-TR|zh-CN|zh-HK|zh-TW,,zh-CN,0x4
ITEM Button2,L200T14W69H24,Restart the computer,CALL SHUTR,,0
ITEM Button3,L200T72W69H24,Command prompt,EXEC CMD.EXE /k @ECHO Tip: Control the startup manager:bcdedit  Main startup code update:bootsect  Startup file repair：bcdboot  
ITEM Button4,L200T102W69H24,Start repair,CALL FIX,,0
_END

_SUB MountESP
FIND $%PROCESSOR_ARCHITECTURE%=x64,EXEC =*MountESP64.EXE!EXEC =*MountESP.EXE
MESS+icon102 Mounted successfully!
KILL \UEFI boot repair
CALL @UEFIX
_END

_SUB SelectWinDrv
BROW Tag,*C:\Windows,Please select a Windows directory：
FIND $%Tag%=,!ENVI WinDrv=%Tag%	
_END

_SUB FIX
FIND $%ESP%=,TEAM MESS+icon0 Please select ESP partition@ ERROR#OK|EXIT
IFEX %WinDrv%,!TEAM MESS+icon0 Please select the root directory of the Windows system @ ERROR#OK|EXIT
MESS     ESPPartition            for %ESP%\n\n    The root directory of the Windows system is %WinDrv%\n\n    The system language is %LAN%\n\n\nClick "Yes" to start the repair, click "No" to return to the modification @ Confirm information#YN
FIND $%YESNO%=NO,EXIT
EXEC =*CMD.EXE /k bcdboot.exe %WinDrv%\ /l %LAN% /s %ESP% /f uefi
MESS+icon102 Repaired successfully！
_END

_SUB SHUTR
MESS Are you sure to restart the computer now? @information#YN
FIND $%YESNO%=NO,EXIT
SHUT R
_END
